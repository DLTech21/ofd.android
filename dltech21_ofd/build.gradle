plugins {
    id 'com.android.library'
}

def gitVersionCode() {
    def cmd = 'git rev-list HEAD --count'
    cmd.execute().text.trim().toInteger()
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        minSdkVersion 26
        targetSdkVersion 29
        versionCode gitVersionCode()
        versionName "1.0"
        ndk {
            abiFilters 'armeabi-v7a', 'x86', 'arm64-v8a'
        }
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    libraryVariants.all { variant ->
        if (variant.buildType.name == 'release') {
            variant.assemble.doLast {
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('release.aar')) {
                        def fileName = "ofdrw-release-${gitVersionCode()}"
                        def outputPath = "../outputs"
                        copy {
                            File temp = new File("../outputs")
                            temp.deleteDir()
                            from outputFile
                            into outputPath
                            rename { fileName + ".aar" }
                        }
                    }
                }
            }
        }
    }
}
//apply plugin: 'com.kezong.fat-aar'
dependencies {
    implementation files('libs/itext5-itextpdf-5.5.11.jar')
    implementation project(':photoview')
//    embed project(path: ':photoview', configuration: 'default')
    implementation "androidx.recyclerview:recyclerview:1.2.0"
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'com.blankj:utilcode:1.30.6'
    implementation group: 'org.apache.commons', name: 'commons-compress', version: '1.20'
    implementation 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'
    api('org.ofdrw:ofdrw-core:2.0.6') {
        exclude group: 'org.bouncycastle', module: 'bcpkix-jdk15on'
        exclude group: 'org.bouncycastle', module: 'bcprov-jdk15on'
    }
    api('org.ofdrw:ofdrw-reader:2.0.6') {
        exclude group: 'org.bouncycastle', module: 'bcpkix-jdk15on'
        exclude group: 'org.bouncycastle', module: 'bcprov-jdk15on'
    }
    compileOnly group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.64'
    compileOnly group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version: '1.64'
}